apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                //artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Text-to-Speech (TTS) engine for the Armenian language.'
                    url = 'https://github.com/albert-grigoryan/ArmTTS-Android'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/albert-grigoryan/ArmTTS-Android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'albert-grigoryan'
                            name = 'Albert Grigoryan'
                            email = 'albert_grigoryan@yahoo.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/albert-grigoryan/ArmTTS-Android.git'
                        developerConnection = 'scm:git:ssh://github.com/albert-grigoryan/ArmTTS-Android.git'
                        url = 'https://github.com/albert-grigoryan/ArmTTS-Android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}